// Begin T2
switch (FEAT_TRAINS, SELF, switch_t2_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_SERIES_KTT);
    return CB_RESULT_NO_TEXT;
}
// Graphics

// Purchase Menu
spriteset (spriteset_t2_purchase, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "t2a")
}

// Vehicle
spriteset (spriteset_t2a, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "t2a")
}
spriteset (spriteset_t2b, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "t2b")
}


switch (FEAT_TRAINS, SELF, switch_t2_articulated_part, extra_callback_info1) {
    1..2: return t2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_t2_cargo_subtype, cargo_subtype) {
    0: return string(STR_AUTOMATIC);
    1: return string(STR_ORIGIN);
    2: return string(STR_FLIP);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_t2_graphics_subtype_auto, position_in_vehid_chain % 6) {
    1: spriteset_t2a;
    4: spriteset_t2b;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_t2_graphics_subtype, cargo_subtype) {
    0: switch_t2_graphics_subtype_auto;
    1: spriteset_t2a;
    2: spriteset_t2b;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_t2_graphics, position_in_articulated_veh % 3) {
    1: switch_t2_graphics_subtype;
    empty_sprites;
}

//Cap

switch (FEAT_TRAINS, SELF, switch_t2_capacity, position_in_articulated_veh % 3) {
    1: return 110;
    return 0;
}


item (FEAT_TRAINS, t2) {
    property {
        // Menu
        name:                           string(STR_NAME_KTT_COACH);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1997,05,11);
        model_life:                     40;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 96;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          140 km/h;
        power:                          0 hp;
        weight:                         56 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULLNAME_KTT_COACH), string(STR_USEDLINE_KTT), string(STR_COMFORT_MEDIUM, 192), string(STR_WITH_AIR_CONDITIONER));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_t2_cargo_subtype;

        // Graphics
        default:                        switch_t2_graphics;
        purchase:                       spriteset_t2_purchase;
        articulated_part:               switch_t2_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_t2_capacity;
        cargo_age_period:               (12 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    12;
        running_cost_factor:            8 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   32;
        purchase_cargo_capacity:        110/3;
        name:                           switch_t2_name;
    }
}

// End T2