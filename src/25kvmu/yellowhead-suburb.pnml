// Begin YELLOWHEAD_SUBURB
// Purchase Menu

spriteset (spriteset_yellowhead_suburb_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("25kvemu/yellowhead", "3049stock-c")
}


switch (FEAT_TRAINS, SELF, switch_yellowhead_suburb_purchase_original, position_in_consist % 3) {
    1: spriteset_yellowhead_suburb_purchase_original;
    empty_sprites;
} 

// Graphics


switch (FEAT_TRAINS, SELF, switch_yellowhead_suburb_articulated_part, extra_callback_info1) {
    1..2: return yellowhead_suburb;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}





item (FEAT_TRAINS, yellowhead_suburb) {
    property {
        // Menu
        name:                           string(STR_NAME_YELLOWHEAD_SUBURB);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_MU);
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        variant_group:                  1984;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1981,04,01);
        model_life:                     60;
        retire_early:                   45;
        vehicle_life:                   35;
        track_type:                     SACA;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;
        loading_speed:                  24 << param_loading_speed;
        cargo_age_period:               160 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          908 kW;
        weight:                         32 ton;
        tractive_effort_coefficient:    0.171;
        cost_factor:                    40;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            10;
        reliability_decay:              10;
        bitmask_vehicle_info:           bitmask(2);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_6, string(STR_ELECTRIC_25KVAC), string(STR_NICKNAME_YELLOWHEAD), string(STR_USEDLINE_KCR), string(STR_MTR_FIRST_CAN_ATTACH_WAGON), string(STR_RELDEC_LOW, 10), string(STR_NORMAL_HEAD_SEAT));
        can_attach_wagon:               muhead_without_first_class;
        start_stop:                     mu3multicar;

        // Graphics
        default:                        switch_yellowhead_normal_wagon;
        purchase:                       switch_yellowhead_suburb_purchase_original;
        articulated_part:               switch_yellowhead_suburb_articulated_part;

        // Stats
        length:                         switch_length_2_7_2;
        speed:                          120;
        power:                          return 0;
        purchase_power:                 return 1218; // HP
        tractive_effort_coefficient:    return 0;
        purchase_tractive_effort_coefficient: return int(0.171*255);
        cargo_capacity:                 switch_yellowhead_capacity;
        cargo_age_period:               160 << param_cargo_decay;
        cost_factor:                    40;
        running_cost_factor:            10  * runningcostfactor() + 20;
        purchase_cargo_capacity:        218/3;
        name:                           switch_yellowhead_name;
    }

    // Multiple Unit Wagons
    livery_override(normal_emucoach) {
        default:                        switch_yellowhead_graphics;
        cargo_capacity:                 switch_yellowhead_capacity;
        power:                          switch_yellowhead_power;
        tractive_effort_coefficient:    switch_yellowhead_traction;
        length:                         switch_length_2_7_2;
        weight:                         switch_yellowhead_weight;
        can_attach_wagon:               mu_of_mtr;
        cargo_age_period:               160 << param_cargo_decay;
        bitmask_vehicle_info:           0;
        articulated_part:               switch_mtr_wagon_articulated_part;
        running_cost_factor:            switch_yellowhead_running() * runningcostfactor() + accostfactor();
    }
}

// End YELLOWHEAD_SUBURB

